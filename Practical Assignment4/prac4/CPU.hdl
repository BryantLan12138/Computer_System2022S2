// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    
    // ALU
    ALU(x=outD, y=outAM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=outALU, zr=zr, ng=ng);
    
    // Registers
    ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);
    DRegister(in=outALU, load=loadD, out=outD);
    PC(in=outA, load=loadPC, inc=true, reset=reset, out[0..14]=pc);

    // Mux
    Mux16(a=outALU, b=instruction, sel=instA, out=inA);
    Mux16(a=outA, b=inM, sel=AMtoALU, out=outAM);

    // decoder
        // loadA logic
        And(a=instC, b=instruction[5], out=destA);
        Or(a=instA, b=destA, out=loadA);


        // start with instA logic
        Not(in=instruction[15], out=instA);

        // loadD logic 
        And(a=instruction[15], b=instruction[4], out=loadD);

        // set inc to true

        // pc load logic (loadPC and inc)
            // if instruction is C
            Not(in=instA, out=instC);

            // JGT condition
                // positive output
                Or(a=zr, b=ng, out=state1);
                Not(in=state1, out=pos);
            And(a=instruction[0], b=pos, out=JGT);

            // JEQ condition
            And(a=instruction[1], b=zr, out=JEQ);

            // JLT
            And(a=instruction[2], b=ng, out=JLT);

            // JGE
            Or(a=JGT, b=JEQ, out=JGE);

            // JNE 
            Or(a=JLT, b=JGT, out=JNE);

            // JLE
            Or(a=JLT, b=JEQ, out=JLE);

            // JMP
            Or(a=JGT, b=JLE, out=JMP);

            // Nested Or structure
            Or(a=JGT, b=JEQ, out=j1);
            Or(a=j1, b=JLT, out=j2);
            Or(a=j2, b=JGE, out=j3);
            Or(a=j3, b=JNE, out=j4);
            Or(a=j4, b=JLE, out=j5);
            Or(a=j5, b=JMP, out=j6);

            And(a=instC, b=j6, out=loadPC);

            // AMtoALU logic 
            And(a=instC, b=instruction[12], out=AMtoALU);

            // loadM logic
            And(a=instC, b=instruction[3], out=writeM);
}